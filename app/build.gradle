plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    buildToolsVersion = "34.0.0-rc3"
    compileSdk 34

    defaultConfig {
        applicationId "com.dessalines.thumbkey"
        minSdk 21
        targetSdkVersion 34
        versionCode 29
        versionName "1.12.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp { arg('room.schemaLocation', "$projectDir/schemas") }
    }

    if(project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD

                // Optional, specify signing versions used
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (DEBUG)'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ['-Xjvm-default=all-compatibility', '-opt-in=kotlin.RequiresOptIn']
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }
    namespace 'com.dessalines.thumbkey'
}

dependencies {
    implementation "androidx.navigation:navigation-compose:2.7.0"
    implementation 'com.github.alorma:compose-settings-ui-m3:0.27.0'

    // To use Kotlin annotation processing tool
    ksp 'androidx.room:room-compiler:2.5.2'

    implementation 'androidx.room:room-runtime:2.5.2'
    annotationProcessor 'androidx.room:room-compiler:2.5.2'

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation('androidx.room:room-ktx:2.5.2')

    // optional - Test helpers
    testImplementation 'androidx.room:room-testing:2.5.2'

    // optional - Paging 3 Integration
    implementation 'androidx.room:room-paging:2.5.2'

    // LiveData
    implementation "androidx.compose.runtime:runtime-livedata:1.5.0"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.compose.ui:ui:1.5.0"
    implementation 'androidx.compose.material3:material3:1.1.1'
    implementation "androidx.compose.material:material-icons-extended:1.5.0"
    implementation 'androidx.compose.material3:material3-window-size-class:1.1.1'
    implementation "androidx.compose.ui:ui-tooling:1.5.0"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "com.louiscad.splitties:splitties-systemservices:3.0.0"
    implementation "com.louiscad.splitties:splitties-views:3.0.0"
}
