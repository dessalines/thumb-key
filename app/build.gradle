plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    buildToolsVersion = "34.0.0"
    compileSdk 34

    defaultConfig {
        applicationId "com.dessalines.thumbkey"
        minSdk 21
        targetSdkVersion 34
        versionCode 60
        versionName "2.6.17"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp { arg('room.schemaLocation', "$projectDir/schemas") }
    }

    if(project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD

                // Optional, specify signing versions used
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (DEBUG)'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ['-Xjvm-default=all-compatibility', '-opt-in=kotlin.RequiresOptIn']
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.8"
    }
    namespace 'com.dessalines.thumbkey'
}

dependencies {

    implementation "com.github.jeziellago:compose-markdown:0.3.5"

    implementation 'androidx.navigation:navigation-compose:2.7.6'
    implementation 'com.github.alorma:compose-settings-ui-m3:1.0.3'

    // To use Kotlin annotation processing tool
    ksp 'androidx.room:room-compiler:2.6.1'

    implementation 'androidx.room:room-runtime:2.6.1'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation 'androidx.room:room-ktx:2.6.1'

    // optional - Test helpers
    testImplementation 'androidx.room:room-testing:2.6.1'

    // optional - Paging 3 Integration
    implementation 'androidx.room:room-paging:2.6.1'

    // LiveData
    implementation 'androidx.compose.runtime:runtime-livedata:1.6.0'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.7.0"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.22'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.compose.ui:ui:1.6.0'
    implementation 'androidx.compose.material3:material3:1.1.2'
    implementation 'androidx.compose.material:material-icons-extended-desktop:1.6.0'
    implementation 'androidx.compose.material3:material3-window-size-class:1.1.2'
    implementation 'androidx.compose.ui:ui-tooling:1.6.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "com.louiscad.splitties:splitties-systemservices:3.0.0"
    implementation "com.louiscad.splitties:splitties-views:3.0.0"

    // Emoji Picker
    implementation "androidx.emoji2:emoji2-emojipicker:1.4.0"
}
